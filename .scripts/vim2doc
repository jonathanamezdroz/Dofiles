#!/usr/bin/env perl
#Author: Yves Chevallier <nowox@x0x.ch>
#Date:   2015-04-04 Sat 11:26 AM 
#Desc:   Extract documentation from VimScript scripts.
use 5.010;
use strict;

my @code = ();
while (<>) {
    my $say = '';
    next if /^""/;                  # Double quote is undocumented section
    s/[{}]{3}\d//;                  # Remove vim's folding markers
    s/^(.*?)\s*"<!(.*)$/"`$1` $2/;  # Comment inline code

    if (/^"(?!")/) {
        $_ = code(@code) and @code = () if @code > 0;
        s|^" ?||g;
        s|^\s+$|$/|gm;
        print;
    } else { 
        push @code, $_ =~ s/[\n\r]//gr;
    }
}
print code(@code);

sub code {
    shift @_ while @_ > 0 and $_[0]   =~ /^\s*$/;
    pop   @_ while @_ > 0 and $_[$#_] =~ /^\s*$/;
    return unless @_ > 0;
    "$/```vimscript$/".join($/, @_)."$/```$/$/"
}
